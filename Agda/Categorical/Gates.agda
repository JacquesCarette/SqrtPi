{-# OPTIONS --without-K --exact-split --allow-unsolved-metas #-}
-- add --safe when there are no more holes

open import Categories.Category -- we need it all
open import Categories.Category.Monoidal using (Monoidal)
open import Categories.Category.Monoidal.Symmetric using (Symmetric)
open import Categories.Category.RigCategory

open import Categorical.SqrtRig

-- Everything is over a SqrtRig
module Categorical.Gates {o â„“ e} {C : Category o â„“ e}
  {MâŠ MÃ— : Monoidal C} {SâŠ : Symmetric MâŠ}
  {SÃ— : Symmetric MÃ—} {R : RigCategory C SâŠ SÃ—} (SR : SqrtRig R) where

  open import Data.Product using (_,_)
  open import Level using (Level)

  -- open import Categories.Functor.Bifunctor using (module Bifunctor)
  open import Categories.Category.Monoidal.Interchange.Braided (Symmetric.braided SâŠ) using (module swapInner)
  import Categories.Category.Monoidal.Reasoning as MonR
  import Categories.Morphism.Reasoning as MR
  
  open import Categorical.Scalars SR

  open Category C -- all of it
  open HomReasoning
  open SqrtRig SR
  open Kit R
  open MR C
  -- open MonR MÃ— using (_âŸ©âŠ—âŸ¨refl)
  open MonR MâŠ using (serializeâ‚‚â‚) renaming (_âŸ©âŠ—âŸ¨refl to _âŸ©âŠ•âŸ¨refl; reflâŸ©âŠ—âŸ¨_ to reflâŸ©âŠ•âŸ¨_; _âŸ©âŠ—âŸ¨_ to _âŸ©âŠ•âŸ¨_)
  
  X : 2Ã—2
  X = ÏƒâŠ•

  P : Scalar â†’ 2Ã—2
  P s = id âŠ•â‚ s

  -- Note: S was already defined in SqrtRig
  Z T H : 2Ã—2
  Z = P -ğŸ™
  T = P Ï‰
  H = Ï‰ â— (X âˆ˜ S âˆ˜ V âˆ˜ S âˆ˜ X)

  -- note that this isn't quite what's in the paper, but it is equivalent
  Midswap : {A B C D : Obj} â†’ (A âŠ•â‚€ B) âŠ•â‚€ (C âŠ•â‚€ D) â‡’ (A âŠ•â‚€ C) âŠ•â‚€ (B âŠ•â‚€ D)
  Midswap = swapInner.from

  Mat : {A : Obj} â†’ 2C âŠ—â‚€ A â‡’ A âŠ•â‚€ A
  Mat = (Î»â‡’ âŠ•â‚ Î»â‡’) âˆ˜ Î´áµ£â‡’

  Matâ»Â¹ : {A : Obj} â†’ A âŠ•â‚€ A â‡’ 2C âŠ—â‚€ A
  Matâ»Â¹ = Î´áµ£â‡ âˆ˜ Î»â‡ âŠ•â‚ Î»â‡
  
  Ctrl : {A : Obj} (m : Endo {A}) â†’ 2C âŠ—â‚€ A â‡’ 2C âŠ—â‚€ A
  Ctrl m = Matâ»Â¹ âˆ˜ (id âŠ•â‚ m) âˆ˜ Mat

  nCtrl : {A : Obj} (m : Endo {A}) â†’ 2C âŠ—â‚€ A â‡’ 2C âŠ—â‚€ A
  nCtrl m = Matâ»Â¹ âˆ˜ (m âŠ•â‚ id) âˆ˜ Mat

  SWAP CX CZ : 2C âŠ—â‚€ 2C â‡’ 2C âŠ—â‚€ 2C
  SWAP = ÏƒâŠ—
  CX = Ctrl X
  CZ = Ctrl Z

  CCX :  2C âŠ—â‚€ 2C âŠ—â‚€ 2C â‡’ 2C âŠ—â‚€ 2C âŠ—â‚€ 2C
  CCX = Ctrl CX

  ------------------------------------------------------------------------
  -- Some properties of the above that are implicitly used in the
  -- proofs of the properties (below).
  --
  -- Matâ»Â¹ is an inverse to Mat (i.e. was defined to be so.
  Mat-invË¡ : {A : Obj} â†’ Matâ»Â¹ {A} âˆ˜ Mat â‰ˆ id
  Mat-invË¡ = begin
    (Î´áµ£â‡ âˆ˜ Î»â‡ âŠ•â‚ Î»â‡) âˆ˜ (Î»â‡’ âŠ•â‚ Î»â‡’) âˆ˜ Î´áµ£â‡’ â‰ˆâŸ¨ center (Equiv.sym SâŠ.âŠ—.homomorphism â—‹
                                                     MÃ—.unitorË¡.isoË¡ âŸ©âŠ•âŸ¨ MÃ—.unitorË¡.isoË¡)  âŸ©
    Î´áµ£â‡ âˆ˜ id âŠ•â‚ id âˆ˜ Î´áµ£â‡’                 â‰ˆâŸ¨ reflâŸ©âˆ˜âŸ¨ elimË¡ SâŠ.âŠ—.identity âŸ©
    Î´áµ£â‡ âˆ˜ Î´áµ£â‡’                             â‰ˆâŸ¨ dr.isoË¡ âŸ©
    id                                     âˆ

  Mat-invÊ³ : {A : Obj} â†’ Mat {A} âˆ˜ Matâ»Â¹ â‰ˆ id
  Mat-invÊ³ = begin
    ((Î»â‡’ âŠ•â‚ Î»â‡’) âˆ˜ Î´áµ£â‡’) âˆ˜ Î´áµ£â‡ âˆ˜ Î»â‡ âŠ•â‚ Î»â‡ â‰ˆâŸ¨ center dr.isoÊ³ âŸ©
    Î»â‡’ âŠ•â‚ Î»â‡’ âˆ˜ id âˆ˜ Î»â‡ âŠ•â‚ Î»â‡             â‰ˆâŸ¨ reflâŸ©âˆ˜âŸ¨ identityË¡ âŸ© 
    Î»â‡’ âŠ•â‚ Î»â‡’ âˆ˜ Î»â‡ âŠ•â‚ Î»â‡                  â‰ˆË˜âŸ¨ SâŠ.âŠ—.homomorphism âŸ©
    (Î»â‡’ âˆ˜ Î»â‡) âŠ•â‚ (Î»â‡’ âˆ˜ Î»â‡)               â‰ˆâŸ¨ (MÃ—.unitorË¡.isoÊ³ âŸ©âŠ•âŸ¨ MÃ—.unitorË¡.isoÊ³) â—‹ SâŠ.âŠ—.identity âŸ©
    id                                     âˆ
  
  ------------------------------------------------------------------------
  -- Properties of Gates (split?)

  -- Lemma lem:gates
  -- (ii)
  -- used in CXÂ²â‰¡id proof
  XÂ²â‰¡id : X ^ 2 â‰ˆ id
  XÂ²â‰¡id = SâŠ.commutative

  -- (iii)
  -- used in CZÂ²â‰¡id proof
  PÂ² : (s : Scalar) â†’ (P s) ^ 2 â‰ˆ P (s ^ 2)
  PÂ² s = begin
    (id âŠ•â‚ s) âˆ˜ (id âŠ•â‚ s) â‰ˆË˜âŸ¨ SâŠ.âŠ—.homomorphism âŸ©
    (id âˆ˜ id) âŠ•â‚ (s âˆ˜ s)  â‰ˆâŸ¨ identityÂ² âŸ©âŠ•âŸ¨refl âŸ©
    id âŠ•â‚ s ^ 2           âˆ

  -- (iv) Show P is invertible for invertible scalars instead of assuming
  P-inv : {s t : Scalar} â†’ t âˆ˜ s â‰ˆ id â†’ P t âˆ˜ P s â‰ˆ id
  P-inv {s = s} {t} tsâ‰ˆid  = begin
    (id âŠ•â‚ t) âˆ˜ (id âŠ•â‚ s) â‰ˆË˜âŸ¨ SâŠ.âŠ—.homomorphism âŸ©
    (id âˆ˜ id) âŠ•â‚ (t âˆ˜ s)  â‰ˆâŸ¨ identityÂ² âŸ©âŠ•âŸ¨ tsâ‰ˆid âŸ©
    id âŠ•â‚ id                  â‰ˆâŸ¨ SâŠ.âŠ—.identity âŸ©
    id                         âˆ

  -- (v)
  -- useful lemma for P-comm
  âŠ—Ê³-comm : âˆ€ {A B} {f g : A â‡’ A} â†’ f âˆ˜ g â‰ˆ g âˆ˜ f â†’ id {B} âŠ•â‚ f âˆ˜ id âŠ•â‚ g â‰ˆ id âŠ•â‚ g âˆ˜ id âŠ•â‚ f
  âŠ—Ê³-comm fgâ‰¡gf = âŸº MâŠ.âŠ—.homomorphism â—‹ reflâŸ©âŠ•âŸ¨ fgâ‰¡gf â—‹ MâŠ.âŠ—.homomorphism
  
  -- used in MatProp: Ctrl-comm (and thus CP-comm)
  P-comm : {s t : Scalar} â†’ s âˆ˜ t â‰ˆ t âˆ˜ s â†’ P s âˆ˜ P t â‰ˆ P t âˆ˜ P s
  P-comm stâ‰¡ts = âŠ—Ê³-comm stâ‰¡ts

  -- (vi)
  PXP : (s : Scalar) â†’ P s âˆ˜ X âˆ˜ P s â‰ˆ s â— X
  PXP s = begin
    (id âŠ•â‚ s) âˆ˜ X âˆ˜ (id âŠ•â‚ s)  â‰ˆâŸ¨ reflâŸ©âˆ˜âŸ¨ SâŠ.braiding.â‡’.commute (id , s) âŸ©
    (id âŠ•â‚ s) âˆ˜ (s âŠ•â‚ id) âˆ˜ X  â‰ˆâŸ¨ pullË¡ (Equiv.sym serializeâ‚‚â‚)  âŸ©
    (s âŠ•â‚ s) âˆ˜ X               â‰ˆË˜âŸ¨ identityÊ³ âŸ©âŠ•âŸ¨ identityÊ³ âŸ©âˆ˜âŸ¨refl âŸ©
    ((s âˆ˜ id) âŠ•â‚ (s âˆ˜ id)) âˆ˜ X â‰ˆË˜âŸ¨ scalar-â—â‰ˆâˆ˜ âŸ©âŠ•âŸ¨ scalar-â—â‰ˆâˆ˜ âŸ©âˆ˜âŸ¨refl âŸ©
    (s â— id âŠ•â‚ s â— id) âˆ˜ X     â‰ˆË˜âŸ¨ â—-distrib-âŠ• âŸ©âˆ˜âŸ¨refl âŸ©
    (s â— (id âŠ•â‚ id)) âˆ˜ X       â‰ˆâŸ¨ â—-assocÊ³ âŸ©
    s â— ((id âŠ•â‚ id) âˆ˜ X)       â‰ˆâŸ¨ â—-congÊ³ (elimË¡ SâŠ.âŠ—.identity) âŸ©
    s â— X                      âˆ

  -- (vii)
  XV-comm : X âˆ˜ V â‰ˆ V âˆ˜ X
  XV-comm = begin
    X âˆ˜ V       â‰ˆË˜âŸ¨ E2 âŸ©âˆ˜âŸ¨refl âŸ©
    (V âˆ˜ V) âˆ˜ V â‰ˆâŸ¨ assoc âŸ©
    V âˆ˜ (V âˆ˜ V) â‰ˆâŸ¨ reflâŸ©âˆ˜âŸ¨ E2 âŸ©
    V âˆ˜ X       âˆ

  -- lemma that makes (viii) and (ix) the same
  CAâˆ˜CBâ‰¡id : {o : Obj} {A B : Endo {o}} â†’ A âˆ˜ B â‰ˆ id â†’ Ctrl A âˆ˜ Ctrl B â‰ˆ id
  CAâˆ˜CBâ‰¡id {A = A} {B} ABâ‰ˆid = begin
    (Matâ»Â¹ âˆ˜ (id âŠ•â‚ A) âˆ˜ Mat) âˆ˜ Matâ»Â¹ âˆ˜ (id âŠ•â‚ B) âˆ˜ Mat   â‰ˆâŸ¨ sym-assoc âŸ©âˆ˜âŸ¨refl âŸ©
    ((Matâ»Â¹ âˆ˜ (id âŠ•â‚ A)) âˆ˜ Mat) âˆ˜ Matâ»Â¹ âˆ˜ (id âŠ•â‚ B) âˆ˜ Mat â‰ˆâŸ¨ cancelInner Mat-invÊ³ âŸ©
    (Matâ»Â¹ âˆ˜ (id âŠ•â‚ A)) âˆ˜ (id âŠ•â‚ B) âˆ˜ Mat                 â‰ˆâŸ¨ center (Equiv.sym SâŠ.âŠ—.homomorphism) âŸ©
    Matâ»Â¹ âˆ˜ (id âˆ˜ id) âŠ•â‚ (A âˆ˜ B) âˆ˜ Mat                    â‰ˆâŸ¨ reflâŸ©âˆ˜âŸ¨ identityÂ² âŸ©âŠ•âŸ¨ ABâ‰ˆid âŸ©âˆ˜âŸ¨refl âŸ©
    Matâ»Â¹ âˆ˜ id âŠ•â‚ id âˆ˜ Mat                                â‰ˆâŸ¨ elim-center SâŠ.âŠ—.identity âŸ©
    Matâ»Â¹ âˆ˜ Mat                                            â‰ˆâŸ¨ Mat-invË¡ âŸ©
    id                                                     âˆ
    
  -- (viii)
  -- useful on its own, but also in CCXÂ²â‰¡id
  CXÂ²â‰¡id : CX ^ 2 â‰ˆ id
  CXÂ²â‰¡id = CAâˆ˜CBâ‰¡id XÂ²â‰¡id

  -- First need that ZÂ²â‰¡id (for CZÂ²â‰¡id)
  ZÂ²â‰¡id : Z ^ 2 â‰ˆ id
  ZÂ²â‰¡id = begin
    P (Ï‰ ^ 4) âˆ˜ P (Ï‰ ^ 4) â‰ˆâŸ¨ PÂ² (Ï‰ ^ 4) âŸ©
    P ((Ï‰ ^ 4) ^ 2)       â‰ˆâŸ¨ reflâŸ©âŠ•âŸ¨ -ğŸ™Â²â‰¡ğŸ™ âŸ©
    P ğŸ™                   â‰ˆâŸ¨ SâŠ.âŠ—.identity âŸ©
    id                    âˆ
  
  -- (ix)
  CZÂ²â‰¡id : CZ ^ 2 â‰ˆ id
  CZÂ²â‰¡id = CAâˆ˜CBâ‰¡id ZÂ²â‰¡id

  -- (x)
  CCXÂ²â‰¡id : CCX ^ 2 â‰ˆ id
  CCXÂ²â‰¡id = CAâˆ˜CBâ‰¡id CXÂ²â‰¡id

  -- (xi)
  XPs : {s t : Scalar} â†’ s âˆ˜ t â‰ˆ id â†’ X âˆ˜ P s â‰ˆ s â— (P t âˆ˜ X)
  XPs {s = s} {t} stâ‰ˆid = begin
    ÏƒâŠ• âˆ˜ (id âŠ•â‚ s)          â‰ˆâŸ¨ SâŠ.braiding.â‡’.commute (id , s) âŸ©
    (s âŠ•â‚ id) âˆ˜ X           â‰ˆË˜âŸ¨ identityÊ³ âŸ©âŠ•âŸ¨ stâ‰ˆid âŸ©âˆ˜âŸ¨refl âŸ©
    (s âˆ˜ id) âŠ•â‚ (s âˆ˜ t) âˆ˜ X â‰ˆË˜âŸ¨ scalar-â—â‰ˆâˆ˜ âŸ©âŠ•âŸ¨ scalar-â—â‰ˆâˆ˜ âŸ©âˆ˜âŸ¨refl âŸ©
    (s â— id) âŠ•â‚ (s â— t) âˆ˜ X â‰ˆË˜âŸ¨ â—-distrib-âŠ• âŸ©âˆ˜âŸ¨refl âŸ©
    s â— (id âŠ•â‚ t) âˆ˜ X       â‰ˆâŸ¨ â—-assocÊ³ âŸ©
    s â— ((id âŠ•â‚ t) âˆ˜ X)     âˆ

  -----------------------------------------------------------------------------
  -- Corrolaries that are used in the proofs "inline"
  cong-P : {s t : Scalar} â†’ (s â‰ˆ t) â†’ P s â‰ˆ P t
  cong-P = MonR.âŠ—-resp-â‰ˆÊ³ MâŠ
  
  SÂ²â‰¡Z : S âˆ˜ S â‰ˆ Z
  SÂ²â‰¡Z = begin
    P i âˆ˜ P i â‰ˆâŸ¨ PÂ² i âŸ©
    P (i ^ 2) â‰ˆâŸ¨ cong-P iÂ²â‰¡-ğŸ™ âŸ©
    P -ğŸ™      âˆ
